package Adventure

// Standard imports
import LinkedList
import HashMap
import RegisterEvents

// Custom imports
import AdventureMarker
import AdventureStartLocation
import AdventureExitLocation
import AdventureCreatureGroup
import CreatureGroupType
import Initialization
import PlayerData
import SpecialEffects

/** Represents the basic information for each adventure */
public abstract class Adventure
    private static int indexCounter = 0

    // Constants
    private constant int index
    private constant string name
    private constant string description
    private constant rect area

    // Lists
    /** Mapping : marker type -> creature group type */
    protected constant HashMap<int, CreatureGroupType> creatureGroupsMap = new HashMap<int, CreatureGroupType>()

    /** List of available start location(s) */
    protected constant LinkedList<AdventureStartLocation> startLocations = new LinkedList<AdventureStartLocation>

    /** List of available exit location(s) */
    protected constant LinkedList<AdventureExitLocation> exitLocations = new LinkedList<AdventureExitLocation>

    /** List of creature groups */
    protected constant LinkedList<AdventureCreatureGroup> creatureGroups = new LinkedList<AdventureCreatureGroup>()

    /** List of generic markers found on the area of the adventure */
    protected constant LinkedList<AdventureMarker> markers = new LinkedList<AdventureMarker>()

    /** List of all units handled in this adventure */
    protected constant group allUnits = CreateGroup()

    construct(string name, string description, rect area)
        this.index = indexCounter
        indexCounter++

        this.name = name
        this.description = description
        this.area = area

    ondestroy
        DestroyGroup(this.allUnits)

    // Accessors
    function getIndex() returns int
        return this.index
    
    function getName() returns string
        return this.name

    function toString() returns string
        return "#{0} - {1}".format(this.index.toString(), this.name)

    function getDescription() returns string
        return this.description

    function getArea() returns rect
        return this.area
    
    // Modifiers
    function addStartLocation(AdventureStartLocation startLoc)
        this.startLocations.add(startLoc)
    
    function addExitLocation(AdventureExitLocation exitLoc)
        this.exitLocations.add(exitLoc)
        
    function addCreatureGroup(AdventureCreatureGroup creatureGroup)
        this.creatureGroups.add(creatureGroup)

    function addMarker(AdventureMarker marker)
        this.markers.add(marker)
    
    // Functions
    /** Returns a randomly chosen start location */
    function pickRandomStartLocation() returns AdventureStartLocation
        if this.startLocations.isEmpty()
            Log.error("Adventure " + this.toString() + " has no start location !")
            return null

        int index = GetRandomInt(0, this.startLocations.size() - 1)
        let sl = this.startLocations.get(index)
        PingMinimap(sl.getPos().x, sl.getPos().y, 5.0)
        return sl

    /** Initialize an adventure's data for later use */
    function initialize()
        Log.info("Adventure " + this.toString() + " : " + this.name)

        // Filter
        UnitFilter filter = (unit u) -> u.getOwner() == PLAYER_GOURS_HOSTILE

        // Search for markers in the area
        group gp = CreateGroup()
        findUnitsInRectMatching(gp, this.area, filter, 0)
        for unit u in gp
            switch u.getTypeId()
                case AdventureCreatureGroup.MARKER_ENTRY
                    this.startLocations.add(new AdventureStartLocation(u.getPos(), u.getFacingAngle()))
                case AdventureCreatureGroup.MARKER_EXIT_LOCATION
                    this.exitLocations.add(new AdventureExitLocation(u.getPos(), u.getFacingAngle(), 64))
                default
                    // Check if this is a marker of a known creatures pack
                    CreatureGroupType creatureGroupType = this.creatureGroupsMap.get(u.getTypeId())
                    if creatureGroupType != null
                        this.creatureGroups.add(new AdventureCreatureGroup(u.getPos(), u.getFacingAngle(), creatureGroupType))
                    else
                        // Store the unit's data into a generic marker
                        this.markers.add(new AdventureMarker(u))

            // Delete the unit
            u.remove()

        // Nettoyage memoire
        Log.debug("Adventure " + this.toString() + " - Nombre de marqueurs repertories : " + gp.size().toString())
        DestroyGroup(gp)

    /** Position every player's character */
    function setup() returns AdventureStartLocation
        Log.debug("Setup " + this.toString())
        // Configure shared view with team
        for player p in ACTIVE_PLAYERS
            PLAYER_COLONS.setPlayerAlliance(p, ALLIANCE_SHARED_VISION, false)

        // Position creatures
        for AdventureCreatureGroup creatureGroup in this.creatureGroups
            creatureGroup.getCreatureGroupType().spawn(this.allUnits, creatureGroup.getPosition(), creatureGroup.getOrientation())
        
        // Hide exit locations
        /*for AdventureExitLocation exitLoc in this.exitLocations
            unit u = createUnit(players[PLAYER_NEUTRAL_PASSIVE], ADVENTURE_EXIT_UNIT_TYPE_ID, exitLoc.getPos(), exitLoc.getOrientation())
            u.hide()
            this.allUnits.addUnit(u)*/
        
        if this.exitLocations.isEmpty()
            Log.error("No exit location for this adventure !")

        // Position players
        //Log.debug("Position players " + this.toString())
        AdventureStartLocation startLocation = this.pickRandomStartLocation()
        vec2 position = startLocation.getPos()
        for player p in ACTIVE_PLAYERS
            let pd = PlayerData.get(p.getId())
            let character = pd.getCharacter()
            character.setPos(position)
            character.setFacing(startLocation.getOrientation())
            p.panCamToTimed(character, 0.0)
            p.selectSingle(character)

            position = position.polarOffset(startLocation.getOrientation(), 32.0)

        return startLocation

    /** Cleanup the adventure */
    function cleanup()
        /*FIXME for unit u from this.allUnits
            u.remove()*/
        
    // Overridable functions
    /** Called when the adventure has been cleanup */
    function onEnd()

    /** Called when the adventure has started */
    function onStarted()

    /** Called when a unit is attacked during this adventure */
    function onUnitAttacked(unit _attacker, unit _target)

    /** Called when a unit dies */
    function onUnitDying(unit _dying)

    /** Called when a unit takes an exit */
    function onUnitExit(unit _exit, unit who)
        let pd = PlayerData.get(who)
        if pd != null
            SpecialEffectType.MASSTELEPORTTARGET.flashWithoutLeak(who.getPos())
            who.setPos(gg_rct_TownEntry.getCenter())
            SpecialEffectType.MASSTELEPORTTARGET.flashWithoutLeak(who.getPos())
            pd.getPlayer().panCamToTimed(who, 0.5)

/** Manages the list of available adventures */
public class AdventureManager
    static constant LinkedList<Adventure> LIST = new LinkedList<Adventure>

    /** The currently ongoing adventure, or null if players are in town */
    private static Adventure currentAdventure = null

    /** Returns the currently ongoing adventure, or null if players are in town */
    static function getCurrent() returns Adventure
        return AdventureManager.currentAdventure

    /** Run the specified adventure. If null, gets back to town */
    static function run(Adventure adventure) returns boolean
        // Eventually stop/cleanup previous adventure
        Adventure previous = AdventureManager.getCurrent()
        if previous != null
            displayMessage(MessageType.QUEST, null, 10, "Fin de l'aventure : " + previous.getName())
            previous.onEnd()

        // Start the new adventure
        AdventureManager.currentAdventure = adventure
        if adventure != null
            Log.debug("Setup : " + adventure.toString())
            adventure.setup()
            adventure.onStarted()
            displayMessage(MessageType.QUEST, null, 10, "Demarrage de l'aventure : " + adventure.getName())

        return true

    /** Run a random adventure. */
    static function runRandom() returns boolean
        if AdventureManager.LIST.isEmpty()
            Log.error("No adventure registered !")
            return false

        int index = GetRandomInt(0, AdventureManager.LIST.size() - 1)
        Adventure adventure = AdventureManager.LIST.get(index)
        Log.debug("Run : " + adventure.toString())
        return run(adventure)

    /** Register the adventure */
    static function register(Adventure adventure)
        adventure.initialize()
        LIST.addAt(adventure, adventure.getIndex())

init
    // Register events
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH) ->
        let adventure = AdventureManager.getCurrent()
        if adventure != null
            adventure.onUnitDying(GetTriggerUnit())

    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ATTACKED) ->
        let adventure = AdventureManager.getCurrent()
        if adventure != null
            adventure.onUnitAttacked(GetAttacker(), GetTriggerUnit())

    // Register when a unit takes an exit
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_UNIT_ORDER) ->
        let exit = GetOrderTargetUnit()
        if exit != null and exit.getTypeId() == AdventureCreatureGroup.MARKER_EXIT_LOCATION
            let adventure = AdventureManager.getCurrent()
            if adventure != null
                let u = GetTriggerUnit()
                if u.getPos().distanceToSq(exit.getPos()) < 256 * 256
                    adventure.onUnitExit(exit, u)
                else
                    u.issuePointOrder("move", exit.getPos())
