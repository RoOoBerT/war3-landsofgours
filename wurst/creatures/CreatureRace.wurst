package CreatureRace

// Standard imports
import LinkedList

// Custom imports
import CreatureStats

/** Represents a race with defined advantages and inconvenients */
public class CreatureRace
    /** Identifier for an ability which represents the race of the unit */
    static constant int ABILITY = 'A017'

    /** Index of the race in the LIST (= ability level - 1) */
    private constant int index

    /** Is this race available to be picked by a player in the beginning of the game ? */
    private constant boolean playerAvailable

    // Stats modifiers
    private constant CreatureStats statsAdded = null
    private constant CreatureStats statsAddedPercent = null

    /** List of all existing races */
    static constant LinkedList<CreatureRace> LIST = new LinkedList<CreatureRace>

    private construct(boolean playerAvailable)
        this.index = LIST.size()
        LIST.addAt(this, index)
        this.playerAvailable = playerAvailable

        LIST.add(this)

    private construct(boolean playerAvailable, CreatureStats statsAdded, CreatureStats statsAddedPercent)
        this.index = LIST.size()
        LIST.addAt(this, index)
        this.playerAvailable = playerAvailable
        this.statsAdded = statsAdded
        this.statsAddedPercent = statsAddedPercent

        LIST.add(this)

    // Accessors
    function isPlayerAvailable() returns boolean
        return this.playerAvailable

    function getStatsAdded() returns CreatureStats
        return this.statsAdded

    function getStatsAddedPercent() returns CreatureStats
        return this.statsAddedPercent

    // Modifiers
    /*function setStatsAdded(CreatureStats statsAdded)
        this.statsAdded = statsAdded

    function setStatsAddedPercent(CreatureStats statsAddedPercent)
        this.statsAddedPercent = statsAddedPercent*/

    /** Retrieves the race of a unit, or null if unspecified */
    static function getUnitRace(unit who) returns CreatureRace
        int level = who.getAbilityLevel(CreatureRace.ABILITY)
        if level > 0
            return LIST.get(level - 1)
        return null

    /** Affiliate the specified unit with this race */
    function setUnitRace(unit who)
        who.addAbility(CreatureRace.ABILITY)
        who.setAbilityLevel(CreatureRace.ABILITY, this.index + 1)

    // Race directory
    static constant CreatureRace CENTAUR = new CreatureRace(true
        , new CreatureStats()
            ..setMoveSpeed(40)
        , null
    )
    static constant CreatureRace ELF = new CreatureRace(true
        , new CreatureStats()
        , null
    )
    static constant CreatureRace EREDAR = new CreatureRace(true)
    static constant CreatureRace HUMAN = new CreatureRace(true
        , new CreatureStats()
        , null
    )
    static constant CreatureRace UNDEAD = new CreatureRace(true)
    static constant CreatureRace ORC = new CreatureRace(true)

    //
    static constant CreatureRace ARACHNID = new CreatureRace(false
        , new CreatureStats()
            ..setDamage(DamageClass.DAMAGE_POISON, 1.0)
        , null
    )
    static constant CreatureRace FROST_MONSTER = new CreatureRace(false
        , new CreatureStats()
            ..setDamage(DamageClass.DAMAGE_FROID, 1.0)
            ..setResistance(DamageClass.DAMAGE_FROID, 1.0)
        , null
    )
