package DotEffect

// Standard imports
import ClosureTimers
import LinkedList

// Custom imports
import Combat
import CreatureStats
import DilatableTime
import SpecialEffects

@configurable public constant real DOT_EFFECTS_UPDATE_PERIOD = 0.1

/** FIXME DoT effect handler */
public interface DotEffectHandler
    function onEffect(DotEffect which, unit who)

public class GenericDotEffectHandler implements DotEffectHandler
    constant unit caster
    constant SpecialEffectType effectType
    constant string attachment = "origin"

    constant CreatureStats stats = null

    construct(unit caster, SpecialEffectType effectType, string attachment, CreatureStats stats)
        this.caster = caster
        this.effectType = effectType
        this.attachment = attachment
        this.stats = stats

    function onEffect(DotEffect _which, unit who)
        if this.stats != null
            onUnitPhysicalAttack(this.caster, this.stats, who)

/** Data related to a damage over time (DoT) effect */
public class DotEffect
    // DoT properties
    /** The target affected by the DoT */
    private constant unit target

    private constant real duration

    /** The time at which the effect will stop */
    private real endTime

    /** The initial unit that casted the spell */
    private constant unit emitter

    /** Period at which DoT emits actions */
    private constant real period

    // Action
    /** DoT effect handler */
    private constant DotEffectHandler effectHandler

    /** Time at which the last effect occured */
    private real lastEffectTime

    // Dynamic data
    /** To put to true to stop the effect */
    private boolean stop = false

    /** Directory of all active AoEs */
    static constant LinkedList<DotEffect> LIST = new LinkedList<DotEffect>

    construct(unit target, real duration, unit emitter, real period, DotEffectHandler effectHandler)
        this.target = target
        this.duration = duration
        this.emitter = emitter
        this.period = max(period, DOT_EFFECTS_UPDATE_PERIOD)
        this.effectHandler = effectHandler

    // Accessors
    function getEndTime() returns real
        return this.endTime

    /** The initial unit that generated the DoT effect */
    function getEmitter() returns unit
        return this.emitter

    /** Terminates the effect */
    function stop()
        this.stop = true

    // Functions
    /** Start handling the effects of this DoT */
    function start()
        let currentTime = TimeDilatation.getModifiedTime(this.target.getPos3Real())
        this.lastEffectTime = currentTime - this.period
        this.endTime = currentTime + duration
        LIST.add(this)

    /** Handles DoT effect. Returns true if it is finished and should be deleted */
    function update() returns boolean
        // Is it still effective ?
        if this.stop
            return true
        real time = TimeDilatation.getModifiedTime(this.target.getPos3Real())
        if time >= this.endTime
            // Finished
            return true

        if this.effectHandler != null
            if time >= this.lastEffectTime + this.period
                this.lastEffectTime += this.period
                this.effectHandler.onEffect(this, this.target)

        return false

init
    // Periodically update current DoTs
    doPeriodically(DOT_EFFECTS_UPDATE_PERIOD) cb -> 
        let iter = DotEffect.LIST.iterator()
        while iter.hasNext()
            DotEffect dot = iter.next()
            if dot.update()
                // Finished !
                destroy dot
                iter.remove()
